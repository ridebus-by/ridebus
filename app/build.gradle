plugins {
    id 'com.android.application'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
}

def getGitCommits = 'git rev-list --all --count'.execute([], rootDir).text.trim().toInteger()
def getGitSha = 'git rev-parse --short HEAD'.execute([], rootDir).text.trim()
def getTimestamp() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'")
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.xtimms.ridebus"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode getGitCommits
        versionName "0.1"

        buildConfigField "String", "COMMIT_COUNT", "\"${getGitCommits}\""
        buildConfigField "String", "COMMIT_SHA", "\"${getGitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${getTimestamp()}\""

        multiDexEnabled = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }

    buildTypes {
        debug {
            applicationIdSuffix = '.debug'
        }
        release {
            multiDexEnabled false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1'

    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.activity:activity-ktx:1.2.3'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.work:work-runtime-ktx:2.5.0'
    implementation 'com.google.android.material:material:1.3.0'
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt 'androidx.lifecycle:lifecycle-compiler:2.3.1'

    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.8'

    implementation 'info.android15.nucleus:nucleus:3.0.0'
    implementation 'info.android15.nucleus:nucleus-support-v7:3.0.0'

    // FlowBinding
    implementation 'io.github.reactivecircus.flowbinding:flowbinding-android:1.0.0'
    implementation 'io.github.reactivecircus.flowbinding:flowbinding-appcompat:1.0.0'
    implementation 'io.github.reactivecircus.flowbinding:flowbinding-recyclerview:1.0.0'
    implementation 'io.github.reactivecircus.flowbinding:flowbinding-swiperefreshlayout:1.0.0'
    implementation 'io.github.reactivecircus.flowbinding:flowbinding-viewpager:1.0.0'

    implementation 'eu.davidea:flexible-adapter:5.1.0'
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0'

    implementation 'com.nightlynexus.viewstatepageradapter:viewstatepageradapter:1.1.0'

    implementation 'com.github.tfcporciuncula.flow-preferences:flow-preferences:1.4.0'

    implementation 'uy.kohesive.injekt:injekt-core:1.16.+'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'dev.chrisbanes.insetter:insetter:0.6.0'

    implementation 'com.mikepenz:aboutlibraries:8.9.0'

    implementation 'com.github.tachiyomiorg:conductor-support-preference:2.0.1'

    implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl:4.3.0'
    implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:4.3.0'

    implementation 'com.bluelinelabs:conductor:2.1.5'
    implementation 'com.bluelinelabs:conductor-support:2.1.5'

    implementation 'io.insert-koin:koin-android:3.0.2'
    implementation 'io.insert-koin:koin-android-ext:3.0.2'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.16.1'

    testImplementation 'org.robolectric:robolectric:3.1.4'
    testImplementation 'org.robolectric:shadows-multidex:3.1.4'
    testImplementation 'org.robolectric:shadows-play-services:3.1.4'
}